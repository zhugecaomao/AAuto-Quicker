//grid 网格控件
namespace web.layout.behavior.grid;

isMultiple = function(ltEle) {
    return ltEle.getAttribute ("multiple") ;
}

onColumnClick = function(ltEle, eleHeaderCell ){ 
    ltEle.postEvent( "onTableHeaderClick", eleHeaderCell.index(), eleHeaderCell); 
}

getCurrentRow = function( elTable ){
    for(i,eleChild in elTable.eachChild() ){
    	if( eleChild.state.current )
    		return  eleChild; 
    } 
}

setCurrentRow = function( elTable, elRow,altState,dblClick,smooth)  {
	var  prev = getCurrentRow( elTable );
	
	var newRowChecked = 0x40/*_HL_STATE_CHECKED*/ | 0x20/*_HL_STATE_CURRENT*/;
	var oldRowChecked = 0x40/*_HL_STATE_CHECKED*/;
	
	if( isMultiple(elTable) ) { 
		if( altState & 2/*_HL_SHIFT_KEY_PRESSED*/ ) { 
			checkAll(elTable, false);
			checkRange(elTable,elRow.index(),true); // from current to new
			oldRowChecked = 0;
		} 
		else{
			if (altState & 1/*_HL_CONTROL_KEY_PRESSED*/) { 
 				setCheckedRow (elTable,elRow, true); // toggle
 				newRowChecked = 0;
			}
			else{
 				checkAll(elTable, false);
				setAnchor(elTable,elRow.index() );
			}
			oldRowChecked = 0;
		}
	}
	
	if( prev ) {
		if( prev != elRow ) 
			prev.modifyState( ,0x20/*_HL_STATE_CURRENT*/ | oldRowChecked ); // drop state flags
	}
	
	elRow.modifyState( newRowChecked ); // set state flags 
	elRow.scrollToView( false,smooth );
	
	elTable.postEvent( dblClick? 0x83/*_HL_TABLE_ROW_DBL_CLICK*/ : 0x82/*_HL_TABLE_ROW_CLICK*/, elRow.index()-1, elRow); 	
}

targetRow = function( elTable, ltTarget)  {
    if( !ltTarget || ltTarget.parent() == elTable)
        return ltTarget;
    
    return targetRow(elTable,ltTarget.parent());
}

targetHeader = function(  eleHeaderRow, ltTarget ) {
	if( !ltTarget || ltTarget.parent() == eleHeaderRow)
   		return ltTarget;
   	return targetHeader(eleHeaderRow,ltTarget.parent());
}

fixedRows = function( elTable ) { 
	return ( tonumber( elTable.fixedrows ) : 0 ) + 1;
}

setCheckedRow = function( elTable,elRow,toggle  ) {
	if( toggle ){
		elRow.state.checked = !elRow.state.checked;
		elRow.state.current = elRow.state.checked;
	}
	else
		elRow.state.checked  = false;
}

getAnchor = function(elTable) {
    var elRow = elTable.queryElement("tr:anchor");
    if( elRow ) return elRow.index();
    return 0;
}

setAnchor = function(elTable,idx)  {
	var elRow = elTable.queryElement("tr:anchor");
	if( elRow ) elRow.state.anchor = false;
	
	elRow = elTable.child(idx);
	if( elRow )
		elRow.modifyState(0x4000/*_HL_STATE_ANCHOR*/,0,false);
}
 
checkRange = function(elTable,idx,check){
	if ( !isMultiple(elTable) ) return;

	var start  = getAnchor(elTable);
	var startIdx = ..math.min(start,idx );
	var endIdx = ..math.max(start,idx );
	
	var fRows  = fixedRows(elTable);
	if( startIdx < fRows ) startIdx = fRows; 
	for(i=endIdx;startIdx;-1){
		var  elRow = elTable.child(i);
		if( !! elRow.isVisible() ){ 
			elRow.state.checked = check;
		}
	} 
} 

checkAll = function( elTable,onOff ) { 
	if( !isMultiple(elTable) ) return;
	
	if(onOff){
		elTable.enumQuery(
			function(ltEle){
			 	if( !ltEle.state.checked )
			 		ltEle.state.checked = true;
			},"tr"
		)
	}
	else {
		elTable.enumQuery(
			function(ltEle){
			 	if( ltEle.state.checked )
			 		ltEle.state.checked = false;
			},"tr:checked"
		)
	} 
}

onMouseDblClick = function (ltTarget,elTable, x,y,mouseParams,isMouseDown) { 

	var elRow = targetRow(elTable,ltTarget );
	
	if( elRow ) {
		if( elRow.index() <  fixedRows(elTable) ) {
  			// click on the header cell
  			var headerCell = targetHeader(elRow,ltTarget);
  			
  			if( headerCell )  
      			owner.onColumnClick(elTable, headerCell);//使用owner对象调用成员函数,使之支持派生的sortableGrid.onColumnClick

  			return mouseParams.button_state == 1/*_HL_MAIN_MOUSE_BUTTON*/;
		}
		
		setCurrentRow(elTable, elRow, mouseParams.alt_state ,!isMouseDown); 
		elTable.updateWindow();
	}
	/*
	else{
		checkAll(elTable, false );
		var  prev = getCurrentRow( elTable );
		if(prev){
			prev.state.current = false; 
		}
	}
	*/
}

var grid = self;
namespace sinking {
	//鼠标以SINKING(捕获模式)向下传递下,使用sinking名字空间响应事件
	
	onMouseDown = function (ltTarget,ltEle,x,y,mouseParams) {
		return grid.onMouseDblClick(ltTarget,ltEle, x,y,mouseParams,true)
	}
}

var keyHandle = {

	[0x28/*_VK_DOWN*/] = function(ltTarget,ltEle,keyCode, altState, keyParams){
		var c = getCurrentRow( ltEle );
		var idx = c? (c.index() + 1):fixedRows(ltEle); 
		
		for(i,elRow in ltEle.eachChild(idx) ){
			if( elRow.isVisible() ) {
				setCurrentRow(ltEle, elRow, altState); 
				return true;
			};
		} 
		
		return true;
	};
	[0x26/*_VK_UP*/] = function(ltTarget,ltEle,keyCode, altState, keyParams){
		 var  c = getCurrentRow( ltEle );
         var idx = c? (c.index() - 1):(ltEle.childCount() - 1);
         var fRows = fixedRows(ltEle);
         
         if (idx < fRows) return true;
         for(i,elRow in ltEle.eachChild(idx,fRows) ){
         	if( elRow.isVisible()) {
				setCurrentRow(ltEle, elRow, altState); 
				return true;
			};
         } 
	};
	[0x21/*_VK_PRIOR*/] = function(ltTarget,ltEle,keyCode, altState, keyParams){
		var trc = ltEle.getRect(1/*_HL_ROOT_RELATIVE*/ | 0x60/*_HL_SCROLLABLE_AREA*/);
		var y = trc.top - (trc.bottom - trc.top);
		var first = fixedRows(ltEle);
		var r;
		 
		for(i,nr in ltEle.eachChild( ltEle.childCount(),first) ){
         	if( nr.style.display != "none" ){ 
         	
         		var  pr = r;
   				r = nr;  
   				if( r.getRect(1/*_HL_ROOT_RELATIVE*/ | 0x20/*_HL_BORDER_BOX*/).top < y ) {
      				// row found
      				if(pr) r = pr; // to last fully visible
      				break;
   				}
         	}
        } 
          
		setCurrentRow(ltEle, r, altState, false, true); 
		return true;
	};
	[0x22/*_VK_NEXT*/] = function(ltTarget,ltEle,keyCode, altState, keyParams){
		var trc = ltEle.getRect( 1/*_HL_ROOT_RELATIVE*/ | 0x60/*_HL_SCROLLABLE_AREA*/);
		var y = trc.bottom + (trc.bottom - trc.top); 
		var r; 
		
		var fRows = fixedRows(ltEle)
		for( i,nr in ltEle.eachChild( fRows ) ){ 
			if( nr.style.display != "none" ) {
   				var  pr = r;
   				r = nr;
   				if( r.getRect( 1/*_HL_ROOT_RELATIVE*/ | 0x20/*_HL_BORDER_BOX*/).bottom > y ) {
      				// row found
      				if(pr) r = pr; // to last fully visible
      				break;
   				}
   			}
		}
		
		setCurrentRow(ltEle, r, altState, false, true);	
		return true;
	};
	[0x24/*_VK_HOME*/] = function(ltTarget,ltEle,keyCode, altState, keyParams){
		var idx = fixedRows(ltEle); 
		if( idx == 1) idx = 2;
		 
		for( i,eleRow in ltEle.eachChild( idx ) ){  
			if( eleRow.isVisible() ){
				setCurrentRow(ltEle, eleRow, altState, false, true);  
				return true;
			}
		}
		return true;
	};
	[0x23/*_VK_END*/] = function(ltTarget,ltEle,keyCode, altState, keyParams){ 
		for( i,row in ltEle.eachChild( ltEle.childCount(),fixedRows(ltEle) ) ){  
			if( row.isVisible() ){
				setCurrentRow(ltEle, row, altState, false, true); 
				return true;
			}
		} 
		return true;
	};
	['A'#] = function(ltTarget,ltEle,keyCode, altState, keyParams){
		if( isMultiple(ltEle) && (altState & 1/*_HL_CONTROL_KEY_PRESSED*/) != 0 ) {
              checkAll(ltEle, true);
              return true;
         }
	};
}
onKeyDown = function (ltTarget,ltEle,keyCode, altState, keyParams) {
	var handle = keyHandle[keyCode];
	if( handle )
		return handle(ltTarget,ltEle,keyCode, altState, keyParams)
}