//嵌入FLASH控件
import win.ui;
/*DSG{{*/
var winform = ..win.form( bottom=352;parent=...;right=511;text="嵌入FLASH控件" )
winform.add( 
btn_xcall={ dr=1;bottom=340;text="调用Flash函数";left=119;top=313;z=4;db=1;right=296;cls="button" };
btnDump={ dr=1;bottom=340;text="显示成员";left=307;top=313;z=2;db=1;right=395;cls="button" };
flashBox={ dr=1;dl=1;bottom=303;right=488;left=19;dt=1;top=16;z=1;db=1;edge=1;cls="custom" };
btnScreen={ dr=1;bottom=341;text="全屏";left=403;top=314;z=3;db=1;right=491;cls="button" }
)
/*}}*/

//创建控件
import com.flash;
var flash = com.flash( winform.flashBox ); 

//响应命令 
flash.command = {
	msgbox = function( args ){
		winform.flashBox.fullscreen(false);
		winform.flashBox.msgbox(args)
	} 
	fullscreen = function( args ){
		..io.print("args",args)
		winform.flashBox.fullscreen( string.cmp(args,"true") == 0 ); 
	} 
}

//导出接口函数 
flash.external = {
	addNumbers = function(a,b){
		winform.flashBox.msgbox("addNumbers被调用,参数:" + a + " " + b) 
		return a + b;
	}	
}
 
//打开swf文件
flash.movie = "\testCall.swf" 
flash.play()

winform.btn_xcall.oncommand = function(id,event){ 
	var str = flash.xcall("callMe",1,2);
	winform.flashBox.msgbox("FLASH函数返回值:" + str  );

}

winform.btnDump.oncommand = function(id,event){
	io.open()
	com.DumpTypeInfo(flash._object)   
	io.print("按 CTRL+C 关闭此窗口")
} 

winform.btnScreen.oncommand = function(id,event){
	winform.flashBox.fullscreen(true);
	
}

winform.show(true) 
win.loopMessage();
return winform;

/***

//testCall.swf 脚本( AS3.0 )

btnCallAAuto.addEventListener(MouseEvent.CLICK, fl_ClickCallAAuto);
function fl_ClickCallAAuto(event:MouseEvent):void {
	fscommand("msgbox", "这是flash调用AAuto的响应");
}
 
import flash.external.*;
function callMe(a:uint,b:uint):uint {
	var result:uint = ExternalInterface.call("addNumbers",a,b);
	return result;
}
ExternalInterface.addCallback("callMe", callMe); 

***/
